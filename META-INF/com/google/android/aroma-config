# Set Colorspace and Display
ini_set("dp","7");
ini_set("force_colorspace", "rgba");

# Initializing Rom Information
ini_set("rom_name",             "Shedrock BQA2 ROM");
ini_set("rom_version",          "1.6.5.0");
ini_set("rom_author",           "wilmans2m");
ini_set("rom_device",           "SM G903F");
ini_set("rom_date",             "22.02.2017");

# Setup Theme
fontresload("0", "ttf/Roboto-Regular.ttf", "12");
fontresload("1", "ttf/Roboto-Regular.ttf", "18");
theme("touchwiz");

###################################################################################################################
# Welcome with Infos
#
viewbox(
	"WELCOME",
	"INSTALL <b>"+
	ini_get("rom_name")+
	"</b> FOR <b>"+ini_get("rom_device")+"</b>.\n\n"+
	"INFORMATION OF THE ROM:\n\n"+
	" THE NAME \t    : <b><#selectbg_g>"+ini_get("rom_name")+"</#></b>\n"+
	" THE VERSION\t    : <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
	" THE DATE\t    : <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n"+
	" THE DEVELOPER: <b><#selectbg_g>"+ini_get("rom_author")+"</#></b>\n"+
	"<b><#f00> ONLY FOR SM-G903F</#></b>\n\n\n"+	
	"PRESS NEXT TO CONTINUE",
"@welcome"
);

###################################################################################################################
#
# TERMS OF USE

agreebox(
    "Important Notice",
    "Please carefully read the Terms of Use!",
    "@agreement",
    resread("read_me.txt"),
    "I agree with these Terms of Use",
    "You must to agree with the Terms of Use to continue"
);

###################################################################################################################
# Main
menubox(
	#-- Title
	"Shedrock Rom Installer",
	#-- Sub Title
	"Please select menu below to start",
	#-- Icon
    "@apps",
	#-- Will be saved in /tmp/aroma/mainmenu.prop
    "mainmenu.prop",
	#-- Items
    "Installation", "Configure ROM and start installation", "@install", #-- selected = 1
    "Changelog", "Show the changelog", "@update", #-- selected = 2
	"Credits", "Show the credits", "@license", #-- selected = 3
    "Exit", "Exit the Installer", "@default" #-- selected = 4
);

###################################################################################################################
# Changelog
if prop("mainmenu.prop", "selected")=="2" then
	textdialog(
		"Changelog",
		resread("changelog.txt"),
		"Close"
	);
	back("1");
endif;

###################################################################################################################
# Credits
if prop("mainmenu.prop", "selected")=="3" then
	textdialog(
		"Credits",
		resread("credits.txt"),
		"Close"
	);
	back("1");
endif;

###################################################################################################################
#Exit
if prop("mainmenu.prop", "selected")=="4" then
	if
		confirm(
			"Exit",
			"Are you sure want to exit the Installer?",
			"@alert"
		)=="yes"
	then
		exit("");
	endif;

	back("1");
endif;
###################################################################################################################

# WIPE

	checkbox(
		"Wipe option",
		"Choose wipe method",
		"@customize",
		"wipe.prop",
		"Select what to wipe:", "",                                2,
		"Data",                 "Without Internal Storage",        0 #1
	);

# firmware
		selectbox(
	      "Firmare Flash",
	      "Flash kernel and modem",
	      "@apps",
	      "firm.prop",
		  
		  "Flash stock BQA2 Kernel ?", 	"",		2,  #-- Group 1 - key = "selected.1"
		  "Yes",					"",		1,	#-- selected.1 = 1
		  "No",						"",		0,	#-- selected.1 = 2

		  "Flash stock BQA2 Modem ?",	"",		2,	#-- Group 2 - key = "selected.2"
		  "Yes",					"",		0,	#-- selected.2 = 1
		  "No",						"",		1	#-- selected.2 = 2
	   );
	
# BUILD PROP
	selectbox(
		"Build.prop",
		"Build.prop Tweaks",
		"@customize",
		"build.prop",
		"Activate", "",					2,	#-- Group 1 - key = "selected.1"
		"Yes",		"",					0,  #-- selected.1 = 1
		"No",		"", 				0   #-- selected.1 = 2
	);	

# BOOT SCRIPT
	selectbox(
		"Boot Script",
		"Select here to execute some Scripts at boot or not",
		"@customize",
		"boot.prop",
				
		"Do you want to execute Boot Scripts ?", 	"", 2, #-- Group 1 - key = "selected.1"
		"Yes",										"",	0, #-- selected.1 = 1
		"No",										"", 0  #-- selected.1 = 2
	);
		
		if (prop("boot.prop","selected.1")=="1")
			then
				checkbox(
					"Boot Scripts",
					"Choose Script to execute at boot",
					"@customize",
					"bootscript.prop",
					
					"Which one ?",			"", 2,
					"File Trimming",		"",	0, #1
					"Better Scrolling",		"",	0, #2
					"Wifi Sleeping",		"",	0, #3
					"SD R/W Tweaks",		"",	0, #4
					"Optimize CPU",			"",	0  #5
				);
	   else
			writetmpfile(
				"bootscript.prop",
				"item.1=0\n"
			);
	   endif;
	   
# ADBLOCK
	selectbox(
		"ADBLOCK",
		"ADBLOCK",
		"@customize",
		"ad.prop",
					
		"Do you want to use the adblock file",	"",				2,	#-- Group 1 - key = "selected.1"
		"Yes",				"Install MoaAB ADBlocking file",	0,  #-- selected.1 = 1
		"No",				"",									1   #-- selected.1 = 2
	);

# BOOTANIM
	selectbox(
		"BOOT ANIMATION",
		"Select here your boot animation",
		"@customize",
		"bootanim.prop",
					
		"Boot Animation available:",	"",	2, #-- Group 1 - key = "selected.1"
		"Marshmallow",						"",	0, #-- selected.1 = 1
		"Piss Off Apple",					"",	0, #-- selected.1 = 2
		"Note 7 black",						"",	0, #-- selected.1 = 3
		"Note 7 White",						"",	0, #-- selected.1 = 4
		"None",								"",	0  #-- selected.1 = 5
	);	
	   
# CSC
	form(
		"CSC",
		"Select here which CSC you want to install to your device:",
		"icons/install",
		"csc.prop",
		
		"CSC",	"CSC Selection",	"",							"group",
		"None",	"None",				"No CSC",					"select",
		"BTU",	"BTU",				"UK",						"select",
		"CYV",	"CYV",				"Cyprus",					"select",
		"DBT",	"DBT",				"Germany",					"select",
		"ITV",	"ITV",				"Italy",					"select",
		"MET",	"MET",				"Ireland",					"select",
		"PRO",	"PRO",				"Belgium",					"select",
		"ROM",	"ROM",				"Romania",					"select",
		"SEE",	"SEE",				"South East Europe",		"select",
		"TPH",	"TPH",				"Portugal",					"select",
		"TWO",	"TWO",				"Croatia",					"select",
		"VGR",	"VGR",				"Greece",					"select",
		"XEF",	"XEF",				"France",					"select",
		"XEH",	"XEH",				"Hungary",					"select",
		"XEO",	"XEO",				"Poland",					"select"
	);

# BLOATWARE		
			
	checkbox(
		"Bloatware - Samsung Applications",
		"Select the apps that you want to install:",
		"@personalize",
		"sm_apps.prop",
	  
		"Applications",                  "", 2,
			"Accuweather",               "", 0, #1
			"All Share",                 "", 0, #2
			"Ant Plus",                  "", 0, #3
			"Clock",                     "", 0, #4
			"Hybrid Radio FM",           "", 0, #5
			"Magic Shot",                "", 0, #6
			"Photo Studio",              "", 0, #7
			"Quick Connect",             "", 0, #8
			"Samsung Account",           "", 0, #9
			"Samsung Billing",           "", 0, #10
			"Samsung Finder",            "", 0, #11
			"Samsung Gear",              "", 0, #12
			"Samsung Health",            "", 0, #13
			"Samsung Mobile Print",      "", 0, #14
			"Samsung Music",             "", 0, #15
			"Samsung One Drive",         "", 0, #16
			"Samsung Push Client",       "", 0, #17
			"Samsung TTS",               "", 0, #18
			"SecurityPolicyData Client", "", 0, #19
			"SmartManager",              "", 0, #20
			"S-Voice",                   "", 0, #21
			"Video Player",              "", 0  #22
	);

#Launcher
	selectbox(
		"LAUNCHER SETTINGS",
		"LAUNCHER SETTINGS",
		"@customize",
		"nova.prop",
		"Select here your favorite Launcher",  "",                  2, #-- Group 1 - key = "selected.1"
			"Stock",                           "Touchwiz",          0, #-- selected.1 = 1
			"Alternate",                       "Nova only",         1, #-- selected.1 = 2
			"Full",                            "Touchwiz and Nova", 0  #-- selected.1 = 3
	);
	
#SMS
	selectbox(
		"SMS, MMS SETTINGS",
		"SMS, MMS SETTINGS",
		"@customize",
		"sms.prop",
		"Select here your favorite SMS application",  "",                  		  2, #-- Group 1 - key = "selected.1"
			"Stock",                           		  "Stock SecMms",          	  0, #-- selected.1 = 1
			"Stock Modded",                       	  "Unlimited sms characters", 1, #-- selected.1 = 2
			"Chomp SMS",                              "Chomp SMS", 		  		  0, #-- selected.1 = 3
			"Signal",                            	  "Signal Private Messenger", 0  #-- selected.1 = 4
	);
	
#Bravia
	selectbox(
		"BRAVIA SETTINGS",
		"BRAVIA SETTINGS",
		"@customize",
		"sony.prop",
		"Do you want to intall Sony Bravia & X-Reality Engine",  "",  2, #-- Group 1 - key = "selected.1"
			"Yes",                           		  			 "",  1, #-- selected.1 = 1
			"No",                            	  				 "",  0  #-- selected.1 = 2
	);
	
# Supersu
	selectbox(
		"Root Settings",
		"Select here which root you want",
		"@customize",
		"supersu.prop",
		"Select your Root Method", "", 2, #-- Group 1 - key = "selected.1"
			"SuperSU",             "", 1, #-- selected.1 = 1
			"Magisk",              "", 0, #-- selected.1 = 2
			"No root",             "", 0  #-- selected.1 = 3
	);
	
# ViperX
	if (prop("supersu.prop","selected.1")=="1")
		then
			selectbox(
				"Options settings",
				"Select your Options:",
				"@customize",
				"optsu.prop",
				
				"Choose your SuperSU version",	"",         					2,	#-- Group 1 - key = "selected.1"
				"2.74",							"working",						1,	#-- selected.1 = 1
				"2.79",							"not working with everybody",	0,	#-- selected.1 = 2
				
				"Do you want to install Viper ARISE ?",				"",         2,	#-- Group 2 - key = "selected.2"
				"Yes",						"Install Viper ARISE",				1,	#-- selected.2 = 1
				"No",						"Don't install Viper",				0,	#-- selected.2 = 2
				
				"Do you want to install Xposed framework ?",	"",         						2,	#-- Group 3 - key = "selected.3"
				"Yes",											"Install Xposed framework",			1,	#-- selected.3 = 1
				"No",											"Don't install Xposed framework",	0	#-- selected.3 = 2
			);
	endif;
		
		if (prop("optsu.prop","selected.3")=="1")
			then
				checkbox(
					"Xposed modules",
					"Choose Xposed modules",
					"@customize",
					"xposedmodules.prop",
					
					"Choose the xposed modules ?",			"",                                         2,
					"Amplify",								"Control Wavelocks",						0, #1
					"Force Doze",							"Enable Doze when turn off screen",			1, #2
					"Greenify",								"Save battery Power",						1, #3
					"Stay Awake",							"Never screen of for choose apps",			0, #4
					"Marshmallow SD Fix",				    "Give Permissions to use External Storage",	0, #5
					"RootCloak",							"Hide Root from choosen apps",				0, #6
					"Xhangouts",							"Extension to Hangouts",					0, #7
					"XInternal SD",							"Change patch to internal SD Card",			0, #8
					"Wanam Xtouchwiz",						"Customize Samsung Touchwiz",				0, #9
					"Power Nap",							"Stop Wakelocks",							0, #10
					"Data Usage",							"Shows Data Usage in status bar",			0, #11
					"Sim Editor",							"Modify recorded Sim number",				0, #12
					"Xposed Tweaks",						"Many Tweaks for Touchwiz",					1, #13
					"Advanced Power Menu +",				"Advanced reboot options",					1, #14
					"Sudo Hide",							"Hide Root",								0, #15
					"No Device Check",						"Disable device check compatibility",		0, #16
					"Serial Number Changer",				"Change Serial Number",						0, #17
					"Android Firewall +",					"AFWall+",									0, #18
					"GravityBox (MM)",						"Gravity Box",								1, #19
					"No Wakelock",							"Disable wakelocks of specific apps",		1  #20
					);
			else
				writetmpfile(
					"xposedmodules.prop",
					"item.1=0\n"
			);	
		endif;			
			
###################################################################################################################
# Install
ini_set("text_next", "Install Now");
ini_set("icon_next", "@installbutton");
viewbox(
	"The installation is ready to start",
	"Press NEXT to start this installation\n"+
	"If you want to change your options, press BACK",
	"@install"
);
ini_set("text_next", "Next");
ini_set("icon_next", "@next");

#setvar("retstatus",
	install(
	"",
	"<b>"+ini_get("rom_name")+"</b> INSTALLATION\n"+
	"PLEASE WAIT while installation process of <b>"+ini_get("rom_name")+
	"</b> It will take some time",	
	"@install",	
	"Installation completed of <b>"+ini_get("rom_name")+
	"</b> Press Next to continue"
#	)
);
ini_set("text_next", "Finish");
ini_set("icon_next", "@finish");

###################################################################################################################
checkviewbox(
	"",
	"<#selectbg_g><b>INSTALLATION COMPLETE</b></#>\n\n"+
	"<b>"+ini_get("rom_name")+"</b> is now installed in your phone",
	"@welcome",
	"Reboot device",
	"1",
	"reboot_it"
);

if
	getvar("reboot_it")=="1"
then
	reboot("onfinish");
endif;
